////////////////////////////////////////////////////////////////////////////////
/* 
	This script converts a column of 8,760 values (hourly data for one year) to 
	a 2-D array of 24 columns and 365 rows a 24-hour by 365-day array.

	The name of the hour-by-day array is hxd.

	Tested in SAM 2020.2.29 r2

*/
////////////////////////////////////////////////////////////////////////////////

// for testing, use output of PVWatts performance model to get a sample 8760
// data set
simulate();
data = get( 'gen' ); // hourly data

dpm = [31,28,31,30,31,30,31,31,30,31,30,31]; // days per month

hoy=0; // hour of year
doy = 0; // day of year

hxd[0][0] = 'Month'; // heading for Column 1
hxd[0][1] = 'Day'; // heading for Column 2
for ( n=0; n<24; n++ ) { hxd[0][n+2] = 'Hour ' + to_string(n+1); } // headings for hour of day columns

for( m = 0; m<12; m++ )
{
	dom = 0; // day of month
	n_days = dpm[m];
	for( d=0; d<n_days; d++ )
	{
		for( h=0; h<24; h++ )
		{
			hxd[doy+1][0] = m+1; // month
			hxd[doy+1][1] = d+1; // day
			hxd[doy+1][h+2] = data[hoy]; // hour by day matrix for one year
			hoy++; 
		}
		doy++;
	}
}

output_file = cwd() + '/test.csv'; // file name to store hxd array
ok = csvwrite( output_file, hxd );
if ( ok ) { browse (output_file ); }
else { msgbox('Unable to write data to CSV file!\nIs the file open in Excel?\n\n' + output_file );}

