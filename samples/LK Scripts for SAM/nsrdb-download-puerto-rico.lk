////////////////////////////////////////////////////////////////////////////////
/*
This script downloads data from NREL's Puerto Rico SHR (Simulated High Resolution)
dataset from the National Solar Radiation Database at
https://developer.nrel.gov/docs/solar/nsrdb/puerto_rico_data_download/.

Instructions:
 
 1. If you do not have an NREL Developer Network key, get one at 
    https://developer.nrel.gov/signup/.
    
 2. Set the values of 'email' and 'key' to your email address and NREL Developer
    Network key as strings.   

 3. Assign values to 'lat' and 'lon' for a location in Puerto Rico.
 
 4. Run the script and follow the prompts to choose a year and time step.
 
 5. After the file downloads, go to the Location and Resource page and click
   'Add/remove weather file folders' to add the folder containing the downloaded
   file(s) to your weather file folders. You can then choose the file from the
   solar resource library and use it for a simulation.
   

Note: The NSRDB may return a file with an error message instead of weather data
if you exceed the the download rate limit or if there is a problem with the URL.

Tested in SAM 2020.2.29 r2

*/
//////////////////////////////////////////////////////////////////////////////*/

// folder to store downloaded files.
path = wfdownloaddir(); // default folder for SAM weather file downloads
if ( !dir_exists( path ) ) 
{
	ok = mkdir( path, false );
	if ( !ok )
	{
		outln( 'Failed to create download folder. Exiting script.' );
		exit;
	}
}

// Set these parameters

// latitude and longitude for a location in Puerto Rico
lat = 18.5;
lon = -66.1;

// NREL Developer API credentials https://developer.nrel.gov/signup/
email = '';
key = '';

// Do not change these parameters

// names
// 1998, 1999, 2000, 2001, 2002, 2003, 2004, 2005, 2006, 2007, 2008, 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
available_years = [1998,1999,2000,2001,2002,2003,2004,2005,2006,2007,2008,2009,2010,2011,2012,2013,2014,2015,2016,2017];
names = choose_from_list( available_years, 'Choose a year:', 'Choose Year', 0 );

// attributes (not required, but request only data SAM uses for simulations)
// air_temperature, clearsky_dhi, clearsky_dni, clearsky_ghi, dhi, dni, ghi, solar_zenith_angle, surface_albedo, surface_pressure, total_precipitable_water, wind_speed
attributes = 'air_temperature,dhi,dni,ghi,surface_albedo,surface_pressure,wind_speed';

// interval
// 5, 30, or 60
available_intervals = [5,30,60];
interval = choose_from_list( available_intervals, 'Choose a time step:', 'Choose Time Step', 2 );

// request url
url = 'https://developer.nrel.gov/api/nsrdb_api/solar/';
url += 'nsrdb_puerto_rico_download.csv?'; // use .json? to download as JSON instead of CSV file
url += '&api_key='+key;
url += '&email='+email;
url += '&names='+names;
url += '&interval='+interval;
url += '&attributes='+attributes;
url +=  '&wkt=POINT(' + to_string(lon) + ' ' + to_string(lat) + ')';

outln( url );

fname = 'nsrdb_puerto_rico_' + lat + '_' + lon + '_' + interval + '_' + names;
f = path + '/' + fname + '.csv';
ok = curl( url, { 'file' = f } );
if ( !ok )
{ 
	outln( 'Download failed.' ); 
}
else 
{
	outln( 'Download succeeded. See ',f); 
	browse( path ); 
}