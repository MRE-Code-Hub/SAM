////////////////////////////////////////////////////////////////////////////////
/*                                                                            

This sample LK script converts a batch of weather files from the TMY2, TMY3
or EPW formats to SAM CSV. 

This script uses the SSC module wfcsvconv to do the conversion, which names the
converted file from informatio in the file header and saves it in the same
folder as the original file.

See the "Solar Resource File Converter Macro" in SAM for LK script that converts
solar resource files from other formats including SolarAnywhere, PVsyst, and
PVGIS.

Instructions:
1. Put the files to convert in a single folder
2. Run the script

Tested in SAM 2020.2.29 r2

*/
////////////////////////////////////////////////////////////////////////////////

// get weather files
dir = choose_dir( cwd(), 'Choose folder with files to convert');
files = dir_list(dir,'*');
for (i=0;i<#files;i++)
{
  files[i] = dir + '/' + files[i];
}
outln('There are ' + #files + ' files in ' + dir);

// convert files
for ( i=0; i<#files; i++ )
{
	wf = files[i];
	obj = ssc_create();
	ssc_var( obj, 'input_file', wf );
	result = ssc_exec( obj, 'wfcsvconv', true );
	if ( result == 0 )
	{
		output = ssc_var( obj, 'output_file');
		if ( file_exists(output) )
		{
			outln( 'Successfully converted ' + file_only( wf ) );
		}
		else
		{
			outln('Failed to convert ' + file_only( wf ) );
		}	
	}
	else
	{
		outln( 'Failed to convert ' + file_only( wf ) + ': ' + result );
	}	
	ssc_free( obj );
}

browse(dir);